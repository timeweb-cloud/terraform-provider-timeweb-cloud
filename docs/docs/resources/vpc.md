---
page_title: "twc_vpc Resource - Timeweb Cloud"
subcategory: ""
description: |-
  Resource for describing needed VPN and provides actual information about its status
---

# twc_vpc (Resource)

Resource for describing needed VPN and provides actual information about its status

## Примеры использования

```terraform
# Example VPC network
resource "twc_vpc" "example-vpc" {
  name        = "Example VPC"
  description = "Some example VPC"
  subnet_v4   = "192.168.0.0/24"
  location    = "ru-1"
}


### Example servers with VPC network

data "twc_presets" "example-preset" {
  price_filter {
    from = 300
    to   = 400
  }
}

data "twc_os" "example-os" {
  name    = "ubuntu"
  version = "22.04"
}

# With VPC network
resource "twc_server" "example-server-with-local-network" {
  name  = "Example server with local network"
  os_id = data.twc_os.example-os.id

  preset_id = data.twc_presets.example-preset.id

  local_network {
    id = twc_vpc.example-vpc.id
  }
}

# With VPC network and specified ID IP
resource "twc_server" "example-server-with-local-network-and-address" {
  name  = "Example server with local network and address"
  os_id = data.twc_os.example-os.id

  preset_id = data.twc_presets.example-preset.id

  local_network {
    id = twc_vpc.example-vpc.id
    ip = "192.168.0.15"
  }
}

### Example database with VPC network
data "twc_database_preset" "example-db-preset-redis-7" {
  location = "ru-1"

  type = "redis"

  disk = 8 * 1024

  price_filter {
    from = 100
    to   = 500
  }
}

resource "twc_database_cluster" "example-redis-7" {
  name = "example_redis7"

  type = "redis7"

  preset_id = data.twc_database_preset.example-db-preset-redis-7.id

  network {
    id = twc_vpc.example-vpc.id
  }
}

### Example load balancer with VPC network

data "twc_lb_preset" "example-lb-preset" {
  requests_per_second = "10K"

  price_filter {
    from = 100
    to   = 200
  }
}

resource "twc_lb" "example-lb" {
  name = "example-lb"

  algo = "roundrobin"

  is_sticky    = false
  is_use_proxy = false
  is_ssl       = false
  is_keepalive = false

  health_check {
    proto = "http"

    port = 80

    path = "/lala"

    inter   = 10
    timeout = 5
    fall    = 3
    rise    = 2
  }

  ips = []

  preset_id = data.twc_lb_preset.example-lb-preset.id

  project_id = resource.twc_project.example-project.id

  local_network {
    id = twc_vpc.example-vpc.id
    ip = "192.168.0.20"
  }
}
```
<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `location` (String) Location for VPC
- `name` (String) Name for VPC
- `subnet_v4` (String) Subnet for VPC in CIDR notation

### Optional

- `description` (String) Description for VPC

### Read-Only

- `id` (String) The ID of this resource.

